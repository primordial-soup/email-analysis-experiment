#!/usr/bin/env perl

use Modern::Perl;
use FindBin::libs;

use EnronBerkeley;
use EnronBerkeley::Message;
use EnronBerkeley::ProjectHelper;

use Text::CSV_XS;
use Path::Tiny;
use Term::ProgressBar;
use List::UtilsBy qw( max_by );

my $toplevel = EnronBerkeley::ProjectHelper->top_level_directory_path;

my $maildir = $toplevel->child(qw(extract data enron-email berkeley enron_with_categories));
my $dataset = EnronBerkeley->new( directory => $maildir );

my $csv_output_path = EnronBerkeley::ProjectHelper->output_file('text-and-labels.csv');
$csv_output_path->parent->mkpath;
my $csv_output = $csv_output_path->openw_utf8;

my $progress = Term::ProgressBar->new({name => 'E-mails', count => $dataset->number_of_instances, remove => 1, ETA => 'linear' });
my $csv = Text::CSV_XS->new( { sep => "\t", binary => 1, eol => $/, auto_diag => 2 } );

my $email_idx = 0;
my $path_iter = $dataset->_message_iterator;

while( defined( my $filename = $path_iter->() ) ) {
	my $file = $filename;
	my $msg = EnronBerkeley::Message->new( file => $file );

	my @cat_top_level_in_3 = grep { $_->top_level_category_id == 3 } @{ $msg->categories };
	if(@cat_top_level_in_3) {
		## The message has to have been categorised in one of the 3.* categories.

		## Which category has the largest count?
		my $largest_cat_3 = max_by { $_->count } @cat_top_level_in_3;

		## Input to learning algorithm
		my $instance = [
			$msg->file->stringify,                      # [instance name]
			$largest_cat_3->second_level_category_id,   # [label]
			$msg->body_single_line                      # [text]
		];

		## Write it out
		$csv->say( $csv_output, $instance );
	}

	$progress->update(++$email_idx);
}

## Done
$csv->eof;
$csv_output->close;
